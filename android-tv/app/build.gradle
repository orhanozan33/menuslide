plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def versionPropsFile = rootProject.file("version.properties")
def versionProps = new Properties()
if (versionPropsFile.exists()) {
    versionProps.load(new FileInputStream(versionPropsFile))
}
def versionCodeNum = versionProps["versionCode"] ? versionProps["versionCode"].toInteger() : 1
def versionNameStr = versionProps["versionName"] ?: "1.0.0"

android {
    namespace 'com.digitalsignage.tv'
    compileSdk 34

    def keystorePropsFile = rootProject.file("keystore.properties")
    if (keystorePropsFile.exists()) {
        def keystoreProps = new Properties()
        keystoreProps.load(new FileInputStream(keystorePropsFile))
        signingConfigs {
            release {
                storeFile rootProject.file(keystoreProps["storeFile"])
                storePassword keystoreProps["storePassword"]
                keyAlias keystoreProps["keyAlias"]
                keyPassword keystoreProps["keyPassword"]
            }
        }
    }

    defaultConfig {
        applicationId "com.digitalsignage.tv"
        minSdk 21
        targetSdk 34
        versionCode versionCodeNum
        versionName versionNameStr
    }

    buildTypes {
        release {
            if (keystorePropsFile.exists()) {
                signingConfig signingConfigs.release
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'

    // ExoPlayer (Media3) - Google Play Services gerektirmez
    implementation 'androidx.media3:media3-exoplayer:1.2.1'
    implementation 'androidx.media3:media3-ui:1.2.1'
    implementation 'androidx.media3:media3-datasource-okhttp:1.2.1'

    // HTTP API çağrıları
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
}
